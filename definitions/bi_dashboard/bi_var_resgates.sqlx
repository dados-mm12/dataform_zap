 config {
    type: "table",
    schema: "bi_dashboard",
    name: "bi_var_resgates",
    description: `gerando tabela de variação de resgates MoM e YoY %`,
    tags: ["resgates","usuarios", "bi","dashboard", "variação","tabela"]
 }

WITH base AS (
  SELECT
    DATE_TRUNC(DATE(Data), MONTH) AS mes,
    COUNT(DISTINCT Id_Resgate) AS novos_resgates,
    COUNT(DISTINCT Email) AS clientes_mes
  FROM ${ref("bi_resgate")}
  WHERE Id_Resgate IS NOT NULL
  GROUP BY 1
),

com_variacoes AS (
  SELECT
    mes,
    novos_resgates,
    clientes_mes,
    -- Resgates
    LAG(novos_resgates) OVER (ORDER BY mes) AS resgates_mes_anterior,
    LAG(novos_resgates, 12) OVER (ORDER BY mes) AS resgates_ano_anterior,
    -- Clientes
    LAG(clientes_mes) OVER (ORDER BY mes) AS clientes_mes_anterior,
    LAG(clientes_mes, 12) OVER (ORDER BY mes) AS clientes_ano_anterior
  FROM base
),

tabela AS(
SELECT
  mes,
  -- Resgates
  novos_resgates,
  SAFE_DIVIDE(novos_resgates - resgates_mes_anterior, resgates_mes_anterior) AS variacao_resgates_mom_pct,
  SAFE_DIVIDE(novos_resgates - resgates_ano_anterior, resgates_ano_anterior) AS variacao_resgates_yoy_pct,
  -- Clientes distintos
  clientes_mes,
  SAFE_DIVIDE(clientes_mes - clientes_mes_anterior, clientes_mes_anterior) AS variacao_clientes_mom_pct,
  SAFE_DIVIDE(clientes_mes - clientes_ano_anterior, clientes_ano_anterior) AS variacao_clientes_yoy_pct

FROM com_variacoes
ORDER BY mes)

SELECT tabela.*, 
CAST(REPLACE(REPLACE(metas.Usuarios_Resgatando, '.', ''), ',', '') AS INT64)
AS meta_usuarios_Resgatando,

tabela.clientes_mes/
CAST(REPLACE(REPLACE(metas.Usuarios_Resgatando, '.', ''), ',', '') AS INT64)
as var_meta_usuarios,

CAST(REPLACE(REPLACE(metas.Resgates_Totais, '.', ''), ',', '') AS INT64)
AS meta_Resgates_Totais,

tabela.novos_resgates/
CAST(REPLACE(REPLACE(metas.Resgates_Totais, '.', ''), ',', '') AS INT64)
as var_meta_totais

FROM tabela
LEFT JOIN ${ref("metas_okr_copia")} AS metas 
ON EXTRACT(MONTH FROM tabela.mes) = CAST(metas.Mes_num AS INT64)
AND EXTRACT(YEAR FROM tabela.mes) = CAST(metas.Ano_num AS INT64)
WHERE metas.Conta = "ZAP"