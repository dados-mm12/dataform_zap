 config {
    type: "table",
    schema: "bi_dashboard",
    name: "bi_var_cadastros",
    description: `gerando tabela de variaçãoo MoM e YoY %`,
    tags: ["cadastro","usuarios", "bi","dashboard", "variação","tabela"]
 }

WITH base AS (
  SELECT
    DATE_TRUNC(DATE(CriadoEm), MONTH) AS mes,
    COUNT(DISTINCT Email) AS novos_usuarios
  FROM ${ref("bi_users")}
  WHERE Email IS NOT NULL
  GROUP BY 1
),

com_variacoes AS (
  SELECT
    mes,
    novos_usuarios,
    LAG(novos_usuarios) OVER (ORDER BY mes) AS usuarios_mes_anterior,
    LAG(novos_usuarios, 12) OVER (ORDER BY mes) AS usuarios_ano_anterior
  FROM base
)
SELECT
  mes,
  novos_usuarios,
  SAFE_DIVIDE(novos_usuarios - usuarios_mes_anterior, usuarios_mes_anterior) AS variacao_mom_pct,
  SAFE_DIVIDE(novos_usuarios - usuarios_ano_anterior, usuarios_ano_anterior) AS variacao_yoy_pct
FROM com_variacoes
ORDER BY mes
